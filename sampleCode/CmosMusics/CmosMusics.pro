# Please do not modify the following two lines. Required for deployment.
include(qtquick2applicationviewer/qtquick2applicationviewer.pri)
qtcAddDeployment()



QT += core gui qml quick sql dbus multimedia

TARGET = cmosmusics
TEMPLATE = app

# Uncomment the next line to build for Desktop (development)
#DEFINES += QML_MEDIA_PLAYER_DESKTOP_BUILD
NAME = cmosmusics
VERSION = -1.0.2
SOURCES += main.cpp
SOURCES += $$files(src/*.cpp)
HEADERS += $$files(src/*.h) \
    src/vorbis.h \
    src/id3v2.h \
    src/id3v1.h \
    src/tags.h \
    src/datadirectory.h

DISTFILES += cmosmusics.spec

RESOURCES += player.qrc

target.path = /opt/cmosmmapp/bin

desktop.path = /usr/share/applications
desktop.files = data/cmosplayer.desktop

icon.path = /usr/share/pixmaps
icon.files = data/cmosplayer.png

INSTALLS += target desktop icon
CONFIG += link_pkgconfig
packagesExist(qdeclarative5-boostable) {
    message("Building with qdeclarative5-boostable support")
    DEFINES += HAS_BOOSTER
    PKGCONFIG += qdeclarative5-boostable
} else {
    warning("qdeclarative5-boostable not available; startup times will be slower")
}

#OTHER_FILES += \
DISTFILES += \
    qml/Titlebar.qml \
    qml/ProgressBar.qml \
    qml/Playing.qml \
    qml/PlayerControls.qml \
    qml/Musiclist.qml \
    qml/Mainpage.qml \
    qml/main.qml \
    qml/InfoPlayerPanel.qml \
    qml/ImageButton.qml \
    qml/TimeBar.qml \
    image/previous_2.png \
    image/previous_1.png \
    image/playing_bg.png \
    image/play_2.png \
    image/play_1.png \
    image/pause_2.png \
    image/pause_1.png \
    image/panel.png \
    image/nocover.png \
    image/next_2.png \
    image/next_1.png \
    image/menu.png \
    image/folder.png \
    image/back.png \
    image/apple.png \
    data/cmosplayer.png \
    data/cmosplayer.desktop

clear.commands += rm -fr *.tar.gz *.rpm rpmbuild
clear.depends = clean distclean

rpm.target = rpm
rpm.commands = \
                                    rm -fr rpmbuild \
                                        && mkdir rpmbuild \
                                        && mkdir rpmbuild/SOURCES \
                                        && mkdir rpmbuild/SPECS \
                                        && mkdir rpmbuild/SRPMS \
                                        && mkdir rpmbuild/RPMS \
                                        && mkdir rpmbuild/BUILD \
                                        && cp *.spec rpmbuild/SPECS/ \
                                        && cp *.tar.gz rpmbuild/SOURCES/ \
                                        && rpmbuild --define=\"_topdir `pwd`/rpmbuild\" -ba --target i586 $${NAME}.spec \
                                        && mv rpmbuild/RPMS/i586/*.rpm . \
                                        && mv rpmbuild/SRPMS/*.rpm . \
                                        && rm -fr rpmbuild  \
                                        && rm -fr .tmp

# order is important, because, "make dist" needs "qrc_*.cpp" which is generated by "make first"
rpm.depends = first dist

QMAKE_EXTRA_TARGETS += rpm clear
